buildscript {
  ext {
    springBootVersion = "1.5.10.RELEASE"
  }
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.10.0"
    classpath "de.thetaphi:forbiddenapis:2.4.1"
    classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.1"
  }
}

apply plugin: "java"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "checkstyle"
apply plugin: "com.github.spotbugs"
apply plugin: "pmd"
apply plugin: "jacoco"
apply plugin: "de.thetaphi.forbiddenapis"
apply plugin: "org.springframework.boot"
apply plugin: "com.diffplug.gradle.spotless"

group = "com.github.wreulicke"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

jacocoTestReport {
  reports {
    xml.enabled true
    csv.enabled true
  }
}

spotless {
  def headerFile = "/** " + project.file("../LICENSE.md").text + "*/"

  java {
    licenseHeader headerFile, "(package|import)"
    eclipse().configFile project.file("eclipse-format-setting.xml")
    trimTrailingWhitespace()
    endWithNewline()
    removeUnusedImports()
  }

  format 'misc', {
    target '**/*.gradle'
    
    indentWithSpaces(2) 
    endWithNewline()
  }
}

checkstyle {
  configFile project.file("checkstyle.xml")
}

forbiddenApisMain {
  bundledSignatures += [
    'jdk-system-out',
    'jdk-unsafe-' + project.sourceCompatibility,
    'jdk-internal-' + project.sourceCompatibility
  ]
  ignoreFailures = false
}

forbiddenApisTest {
  bundledSignatures += [
    'jdk-system-out',
    'jdk-internal-' + project.sourceCompatibility
  ]
  ignoreFailures = false
}

spotbugs {
  toolVersion = '3.1.2'
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

pmd {
  consoleOutput = true
  toolVersion = '5.5.2'
}

pmdMain {
  ruleSetFiles = project.files("pmd.xml")
  ruleSets = []  
}

pmdTest {
  ruleSetFiles = project.files("pmd.xml")
  ruleSets = []  
}

def defaultEncoding = 'UTF-8'

tasks.withType(AbstractCompile).each {
  it.options.encoding = defaultEncoding
  it.options.compilerArgs << '-parameters'
}


bootRun {
  if (project.hasProperty('args')) {
    args project.args.split('\\s+')
  }
}

dependencies {
  compile "org.springframework.boot:spring-boot-starter-actuator"
  compile "org.springframework.boot:spring-boot-starter-data-jpa"
  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-security"
  compile "org.springframework.boot:spring-boot-starter-aop"

  compile "com.zaxxer:HikariCP:2.7.8"
  
  compile "com.fasterxml.jackson.module:jackson-module-parameter-names"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"
  compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
  compile "org.zalando:logbook-spring-boot-starter:1.6.0"

  compile "io.micrometer:micrometer-spring-legacy:1.0.1"

  compile "io.micrometer:micrometer-registry-prometheus:1.0.1"

  compile "org.flywaydb:flyway-core"

  compileOnly "org.projectlombok:lombok:1.16.20"

  runtime "org.springframework.boot:spring-boot-devtools"
  runtime "mysql:mysql-connector-java"

  testCompile "com.wix:wix-embedded-mysql:4.1.0"

  testCompile "org.springframework.boot:spring-boot-starter-test"
  testCompile "org.springframework.security:spring-security-test"
  compile "com.h2database:h2:1.4.196"

  testCompile "org.assertj:assertj-core:3.9.0"
  testCompile "org.mockito:mockito-core:2.15.0"

}
