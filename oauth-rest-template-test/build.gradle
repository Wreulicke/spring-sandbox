buildscript {
  ext {
    springBootVersion = "1.5.14.RELEASE"
    wireMockVersion = "2.18.0"
  }
  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:3.13.0"
    classpath "de.thetaphi:forbiddenapis:2.5"
    classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.2"
    classpath "io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE"
  }
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "com.github.spotbugs"
apply plugin: "jacoco"
apply plugin: "de.thetaphi.forbiddenapis"
apply plugin: "org.springframework.boot"
apply plugin: "com.diffplug.gradle.spotless"
apply plugin: "io.spring.dependency-management"

group = "com.github.wreulicke"
version = "0.0.1-SNAPSHOT"
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
}

jacocoTestReport {
  reports {
    xml.enabled true
    csv.enabled true
  }
}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter', 'junit-vintage'
  }
}

spotless {
  def headerFile = "/** " + project.file("../LICENSE.md").text + "*/"

  java {
    licenseHeader headerFile, "(package|import)"
    eclipse().configFile project.file("eclipse-format-setting.xml")
    trimTrailingWhitespace()
    endWithNewline()
    removeUnusedImports()
  }

  format 'misc', {
    target '*.gradle'

    indentWithSpaces(2)
    endWithNewline()
  }
}


forbiddenApisMain {
  bundledSignatures += [
    'jdk-system-out',
    'jdk-unsafe-' + project.sourceCompatibility,
    'jdk-internal-' + project.sourceCompatibility
  ]
  ignoreFailures = false
}

forbiddenApisTest {
  bundledSignatures += [
    'jdk-system-out',
    'jdk-unsafe-' + project.sourceCompatibility,
    'jdk-internal-' + project.sourceCompatibility
  ]
  ignoreFailures = false
}

spotbugs {
  toolVersion = '3.1.5'
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

def defaultEncoding = 'UTF-8'

tasks.withType(AbstractCompile).each {
  it.options.encoding = defaultEncoding
}


bootRun {
  if (project.hasProperty('args')) {
    args project.args.split('\\s+')
  }
}

dependencyManagement {
  imports {
    mavenBom "org.zalando:tracer-bom:0.17.0"
  }
}


dependencies {
  compile "org.springframework.security.oauth:spring-security-oauth2"
  compile "org.slf4j:slf4j-api"
  compile "org.slf4j:jul-to-slf4j"
  compile "org.slf4j:jcl-over-slf4j"
  compile "org.slf4j:log4j-over-slf4j"
  compile "ch.qos.logback:logback-classic"

  compile "org.springframework.boot:spring-boot-starter-web"
  compile "org.springframework.boot:spring-boot-starter-logging"

  testCompile "org.assertj:assertj-core:3.9.0"
  testCompile "org.mockito:mockito-core:2.15.0"

  testCompile "junit:junit"

  testCompile 'org.junit.jupiter:junit-jupiter-api:5.1.0'
  testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.1.0'
  testRuntime 'org.junit.vintage:junit-vintage-engine:5.1.0'

  testCompile "com.github.tomakehurst:wiremock-standalone:$wireMockVersion"
}


configurations.all {
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.name == "commons-logging") {
      // prefer "jcl-over-slf4j" over "commons-logging":
      details.useTarget "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    }
    if (details.requested.name == "log4j") {
      // prefer "log4j-over-slf4j" over "log4j":
      details.useTarget "org.slf4j:log4j-over-slf4j:$slf4jVersion"
    }
  }
}
